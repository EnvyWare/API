plugins {
    id 'idea'
    id 'java'
    id 'signing'
    id 'com.gradleup.shadow' version '8.3.6'
    id 'maven-publish'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

idea.module { downloadJavadoc = downloadSources = true }

subprojects {
    apply plugin: 'java'
    apply plugin: 'signing'
    apply plugin: 'com.gradleup.shadow'
    apply plugin: 'maven-publish'

    group = 'com.envyful.api'
    version = '8.0.3'

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    javadoc {
        options.encoding = 'UTF-8'
    }

    publish.dependsOn(':commons:test')
    publish.dependsOn("javadoc")

    afterEvaluate {
        publishing {
            repositories {
                maven {
                    name = "envyware"
                    url = uri("https://maven.envyware.co.uk/releases")
                    credentials(PasswordCredentials)
                    authentication {
                        basic(BasicAuthentication)
                    }
                }
            }
            publications {
                maven(MavenPublication) {
                    groupId = "com.envyful.api"
                    artifactId = project.name
                    version = project.version
                    from components.java
                }
            }
        }
    }

    repositories {
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://repo.lucko.me/' }
        maven { url = "https://repo.aikar.co/content/groups/aikar/" }
        maven { url = "https://jitpack.io" }
        maven {
        	name = 'sponge'
        	url = 'https://repo.spongepowered.org/maven'
    	}
        maven {
            url = "https://maven.envyware.co.uk/releases"
        }

        maven { url = 'https://maven.neoforged.net/releases' }
    }
}
